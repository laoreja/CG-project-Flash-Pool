<!DOCTYPE html>
<html>
<head>
    <meta charset='utf-8'/>
    <script type="text/javascript" src="libs.js"></script>
    <script type="text/javascript" src="script.js"></script>
    <script type="text/javascript" src="../lib/gl-matrix.js"></script>
    <script type="text/javascript" src="../lib/webgl-utils.js"></script>
    <script type="text/javascript" src="../lib/cannon.js"></script>

    <script id="shader-vertex-source-shadow" type="x-shader/x-vertex">
        attribute vec3 position;
        uniform mat4 Pmatrix, Lmatrix;
        varying float vDepth;

        void main(void) {
            vec4 position = Pmatrix * Lmatrix * vec4(position, 1.);
            float zBuf = position.z / position.w; //Z-buffer between -1 and 1
            vDepth = 0.5 + zBuf * 0.5; //between 0 and 1
            gl_Position = position;
        }
    </script>

    <script id="shader-fragment-source-shadow" type="x-shader/x-fragment">
        precision mediump float;
        varying float vDepth;

        void main(void) {
            gl_FragColor=vec4(vDepth, 0.,0.,1.);
        }
    </script>

    <script id="shader-vertex-source" type="x-shader/x-vertex">
        attribute vec3 position, normal;
        attribute vec2 uv;
        uniform mat4 Pmatrix, Vmatrix, Mmatrix, Lmatrix, PmatrixLight;
        varying vec2 vUV;
        varying vec3 vNormal, vLightPos;

        void main(void) {
            //Shadow mapping :
            vec4 lightPos = Lmatrix * vec4(position, 1.);
            lightPos = PmatrixLight * lightPos;
            vec3 lightPosDNC = lightPos.xyz / lightPos.w;
            vLightPos = vec3(0.5,0.5,0.5) + lightPosDNC * 0.5;

            gl_Position = Pmatrix * Vmatrix * Mmatrix * vec4(position, 1.);

            vNormal=normal;
            vUV=uv;
        }
    </script>

    <script id="shader-fragment-source" type="x-shader/x-fragment">
        precision mediump float;
        uniform sampler2D sampler, samplerShadowMap;
        uniform vec3 source_direction;
        varying vec2 vUV;
        varying vec3 vNormal, vLightPos;
        const vec3 source_ambient_color = vec3(1.,1.,1.);
        const vec3 source_diffuse_color = vec3(1.,1.,1.);
        const vec3 mat_ambient_color = vec3(0.3,0.3,0.3);
        const vec3 mat_diffuse_color = vec3(1.,1.,1.);
        const float mat_shininess = 10.;

        void main(void) {
            vec2 uv_shadowMap = vLightPos.xy;
            vec4 shadowMapColor = texture2D(samplerShadowMap, uv_shadowMap);
            float zShadowMap = shadowMapColor.r;
            float shadowCoeff = 1.-smoothstep(0.002, 0.003, vLightPos.z-zShadowMap);

            vec3 color = vec3(texture2D(sampler, vUV));
            vec3 I_ambient = source_ambient_color * mat_ambient_color;
            vec3 I_diffuse = source_diffuse_color * mat_diffuse_color * max(0., dot(vNormal, source_direction));

            vec3 I = I_ambient + shadowCoeff * I_diffuse;
            gl_FragColor = vec4(I * color, 1.);
        }
    </script>
</head>
<body style='margin:0' onload='webGLStart()'>
    <canvas id="your_canvas" style="background-color: black;" width="500" height="500"></canvas>
</body>
</html>